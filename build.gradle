plugins {
    id 'fabric-loom'
    id 'maven-publish'
    id "org.jetbrains.kotlin.jvm"
}

version = "${project.mod_version}+${minecraft_version_group}"
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven { url "https://maven.terraformersmc.com/" }
    maven { url "https://maven.shedaniel.me/" }
    maven { url "https://maven.nucleoid.xyz/" }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url "https://maven.siphalor.de/" }
}

loom {
    splitEnvironmentSourceSets()

    mods {
        "homabric" {
            sourceSet sourceSets.main
            sourceSet sourceSets.client
        }
    }
}

Set<String> fabricApiModules = [
        "fabric-api-base",
        "fabric-command-api-v2",
        // Required libraries for Server Translations API
        "fabric-resource-loader-v0",
]

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    fabricApiModules.forEach {
        include(modImplementation(fabricApi.module(it, project.fabric_version)))
    }

    modImplementation include("net.fabricmc:fabric-language-kotlin:${project.fabric_kotlin_version}")
    modImplementation include("me.lucko:fabric-permissions-api:0.2-SNAPSHOT")
    modImplementation include("eu.pb4:sgui:1.2.1+1.19.3")
    modImplementation include("com.github.Lortseam.completeconfig:base:2.3.1")
    modImplementation include("xyz.nucleoid:server-translations-api:2.0.0-beta.2+1.19.4-pre2")
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release = 17
}

java {
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}"}
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}

compileKotlin.kotlinOptions.jvmTarget = "17"
